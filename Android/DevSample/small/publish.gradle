buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

apply from: 'base.gradle'

//--------------------------------------------------------------------------------------------------
// Scripts to upload plugin to Bintray/JCenter [https://bintray.com/]
// > ./gradlew bintrayUpload
//--------------------------------------------------------------------------------------------------
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.github.dcendents.android-maven'

buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'small'

def mavenDesc = 'A small framework to split android app into small parts.'
def siteUrl = 'https://github.com/wequick/Small'
def gitUrl = "${siteUrl}.git"
def issueUrl = "${siteUrl}/issues"

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2015'

def username = 'Galen Lin'

install {
    repositories {
        mavenInstaller {
            pom.artifactId = archivesBaseName
            pom.project {
                packaging 'aar'
                name mavenDesc
                url siteUrl

                inceptionYear inception

                licenses {
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }
                developers {
                    developer {
                        name username
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.locale = 'en_US'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray upload
bintray.user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : 
        System.getenv('BINTRAY_USER')
bintray.key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : 
        System.getenv('BINTRAY_API_KEY')
bintray.apiUrl = 'https://api.bintray.com'
bintray.configurations = ['archives']
bintray.pkg {
    repo = "maven"
    name = "net.wequick.small:small"
    desc = mavenDesc
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    issueTrackerUrl = issueUrl
    licenses = licenseIds
    labels = ['android', 'plugin', 'bundle', 'dynamic', 'small']
    publish = true
    publicDownloadNumbers = true
}
